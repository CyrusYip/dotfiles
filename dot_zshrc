# powerlevel10k instant prompt[[[1
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi
# ]]]
# history[[[1
HISTFILE=~/.histfile
HISTSIZE=10000
SAVEHIST=10000
# ]]]
# auto complete, prompt, editor[[[1
# completion, prompt
autoload -Uz compinit promptinit
compinit
promptinit
prompt walters
# editor
export EDITOR='vim'
# lowercase matches uppercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' menu select
# enable comment
setopt interactive_comments
# ]]]
# keybindings[[[1
# vi keybindings in menu
setopt autocd
bindkey -v
zmodload zsh/complist
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# control arrow to jump words
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word
# ]]]
# powerlevel10k[[[1
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
# ]]]
# delayed commands[[[1
delayed_commands() {
  # PATH
  export PATH=~/bin:$PATH
  if [ -d ~/.nix-profile/bin ]; then export PATH=~/.nix-profile/bin:$PATH; fi
  # use exa
  if (( $+commands[exa] )); then
    alias ls='exa'
  fi
  # zoxide
  if (( $+commands[zoxide] )); then
    init_z() {
      eval "$(zoxide init zsh)"
    }
  fi
  # thefuck
  if (( $+commands[thefuck] )); then
    init_fuck(){
      eval $(thefuck --alias)
    }
  fi
  # nvm
  init_nvm() {
    export NVM_DIR="$HOME/.nvm"
    [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh" --no-use # This loads nvm
    [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
  }
  # miniconda
  init_conda() {
    [ -f /opt/miniconda/etc/profile.d/conda.sh ] && source /opt/miniconda/etc/profile.d/conda.sh
  }
  # lazyload function
  lazyload() {
    for i in "$@"
    do
      eval "
        $i() {
            unfunction $i
            eval init_$i
            $i \$@
        }
      "
    done
  }
  lazyload z fuck nvm conda
}
# ]]]
# load plugins if zinit installed[[[1
if [[ -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
# zinit initialization[[[2
### Added by Zinit's installer
# if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
#     print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
#     command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
#     command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
#         print -P "%F{33} %F{34}Installation successful.%f%b" || \
#         print -P "%F{160} The clone has failed.%f%b"
# fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

### End of Zinit's installer chunk
# ]]]
# plugins[[[2
zinit ice depth=1; zinit light romkatv/powerlevel10k
zinit light romkatv/zsh-defer

zinit wait lucid light-mode for \
  atinit"zicompinit; zicdreplay" \
    zdharma-continuum/fast-syntax-highlighting \
  atload"_zsh_autosuggest_start" \
    zsh-users/zsh-autosuggestions \
  blockf atpull'zinit creinstall -q .' \
    zsh-users/zsh-completions \
    kutsan/zsh-system-clipboard \
    OMZL::directories.zsh \
    OMZP::git
    # zdharma-continuum/history-search-multi-word \

if (( $+commands[fzf] )); then
  zinit wait lucid light-mode for \
    Aloxaf/fzf-tab \
    joshskidmore/zsh-fzf-history-search
  # disable sort
  zstyle ':completion:*' sort false
fi
# ]]]
# run delayed_commands
zsh-defer delayed_commands
else
  delayed_commands
fi
# ]]]
# set fold and filetype in vim
# vim: foldmethod=marker foldmarker=[[[,]]] filetype=zsh
